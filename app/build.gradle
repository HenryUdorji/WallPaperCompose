plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def localPropertiesFile = rootProject.file("local.properties")

// Initializes a new Properties() object called localProperties.
def localProperties = new Properties()

// Loads the local.properties file into the localProperties object.
localProperties.load(new FileInputStream(localPropertiesFile))

def versionMajor = 1 // range: 0 - 99
def versionMinor = 0 // range: 0 - 99
def versionPatch = 0 // range: 0 - 99

def date = new Date().format("yyMMdd")

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.hashconcepts.wallpapercompose"
        minSdk 26
        targetSdk 31
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 1
        // range: 10000 - 999999
        versionName "HashConcepts ${versionMajor}.${versionMinor}.${versionPatch}-${date}-wallpaper-compose"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file(localProperties['SIGNING_JKS_FILE_PATH'])
            storePassword localProperties['SIGNING_KEYSTORE_PASSWORD']
            keyAlias localProperties['SIGNING_KEY_ALIAS']
            keyPassword localProperties['SIGNING_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Compose dependencies
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0-alpha06"
    implementation "androidx.navigation:navigation-compose:2.5.0-alpha04"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'

    // Coroutine Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:2.40.1"
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Room
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"
    //kotlin extension and coroutine support for room
    implementation "androidx.room:room-ktx:2.4.2"

    //Splash screen
    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
}